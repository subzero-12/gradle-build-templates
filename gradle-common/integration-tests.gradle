def objenesis_version = project.properties['objenesisVersion'] ?: "3.1"
def hamcrest_all_version = project.properties['hamcrestAllVersion'] ?: "1.3"
def assertj_version = project.properties['assertJVersion'] ?: "3.14.0"
def junit_tool_box_version = project.properties['junitToolBoxVersion'] ?: "2.4"
def junit_jupiter_testcontainers_version = project.properties['junitJupiterTestcontainersVersion'] ?: "1.13.0"
def integrationTest_spring_profile = project.properties['integrationTestSpringProfile'] ?: "integrationTest"
def integrationTest_source_root = project.properties['integrationTestSourceRoot'] ?: "src/integration-test"

sourceSets {
    integrationTest {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDirs = ["${integrationTest_source_root}/java"]
        }
        resources {
            srcDirs = ["${integrationTest_source_root}/resources"]
        }
    }
}

configurations {
    integrationTestImplementation.extendsFrom testImplementation
    integrationTestRuntime.extendsFrom testRuntime
    implementation.exclude group: 'ch.qos.logback'
}

task integrationTest(type: Test) {
    useJUnitPlatform()
    environment SPRING_PROFILES_ACTIVE: environment.SPRING_PROFILES_ACTIVE ?: "${integrationTest_spring_profile}"

    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
}

dependencies {
    integrationTestImplementation group: 'org.objenesis',                name: 'objenesis',     version: "${objenesis_version}"
    integrationTestImplementation group: 'org.hamcrest',                 name: 'hamcrest-all',  version: "${hamcrest_all_version}"
    integrationTestImplementation group: 'org.assertj',                  name: 'assertj-core',  version: "${assertj_version}"
    integrationTestImplementation group: 'com.googlecode.junit-toolbox', name: 'junit-toolbox', version: "${junit_tool_box_version}"
    integrationTestImplementation group: 'org.testcontainers',           name: 'junit-jupiter', version: "${junit_jupiter_testcontainers_version}"
}